* Simulate dummy MTO data from descriptive statistics ;
%LET folder = /folders/myfolders;
%LET outputs = &folder/outputs;
%LET mto = &folder/simulated/MTO/data;
%LET nber = &mto/NBER;
libname OUTPUTS "&outputs";
libname MTO "&mto";
libname NBER "&nber";
proc iml;
  N = 5000 ;
  * Create the empty data set ;
  use OUTPUTS.preimp_numdesc;
    read all var{varname};
  close;
  numvars = unique(varname);
  use OUTPUTS.preimp_chardesc;
    read all var{varname};
  close;
  charvars = unique(varname);
  idx = 1:N; * allocate ;
  call randseed(2016);
  * Define char types and lengths for the charvars ;
  f_svy_final_desc = "........................................";
  f_svy_final_disp = ".....";
  f_svy_sample2007 = "..";
  create NBER.Mto_jama_preimp_20160111 var ({'ppid'} || numvars || charvars);
    * Fill the data set with simulated data ;
    ppid = 1:N;
    do i=1 to ncol(numvars);
      call execute("use OUTPUTS.preimp_numdesc where(varname='"+numvars[i]+"');");
        read all var{val count};
        call randgen(idx, "table", count/sum(count));
        call execute(numvars[i]+"= val[idx];");
      close;
    end;
    do i=1 to ncol(charvars);
      call execute("use OUTPUTS.preimp_chardesc where(varname='"+charvars[i]+"');");
        read all var{val count};
        call randgen(idx, "table", count/sum(count));
        call execute(charvars[i]+"= val[idx];");
      close;
    end;
    append;
  close  NBER.Mto_jama_preimp_20160111;
