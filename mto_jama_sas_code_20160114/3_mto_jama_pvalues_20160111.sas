/* 
Program Name: 03_mto_jama_impute_data_YYYYMMDD.sas
Modified by: Matt Sciandra from program called "Calc_FDR.sas" created by Ron Kessler's team at Harvard Medical School.  
Last Modified: 1/11/16
Purpose: To use the Benjamini-Hochberg method to adjust for the false discovery rate the p-values of the significance of the odds ratios presented in Tables 4 and 5 of the Moving to 
		 Opportunity (MTO) paper in the Journal of the American Medical Assocation (JAMA) called "Associations of Housing Mobility Interventions for Children in High-Poverty Neighborhoods 
		 With Subsequent Mental Disorders During Adolescence".

Notes:
- The p-values fed into the call of the "calcfdr" macro defined below are generated by the latest version of the SAS program that runs the main analysis for the paper: 03_mto_jama_assoc_YYYMMDD.sas 
	+ The p-values are output into separate Excel files for Tables 4 and 5 
	+ The p-values are rounded to the third decimal place before being fed into the "calcfdr" macro
*/

** Generate macro to perform the adjustment;
%MACRO calcfdr(rowlab,col,p1,p2,p3,p4,p5,p6,p7);

 DATA raw_p;

  raw_p=0;
  DO i=1 TO 7;
   OUTPUT;
  END;

 DATA raw_p;
  length rowlab $25.; 
  SET raw_p;
  rowlab = "&rowlab";
  IF _N_=1 THEN raw_p=&p1;
  IF _N_=2 THEN raw_p=&p2;
  IF _N_=3 THEN raw_p=&p3;
  IF _N_=4 THEN raw_p=&p4;
  IF _N_=5 THEN raw_p=&p5;
  IF _N_=6 THEN raw_p=&p6;
  IF _N_=7 THEN raw_p=&p7;
 DROP i; 
RUN;


proc multtest inpvalues = raw_p holm bonferroni fdr dependentfdr;
 test fisher(raw_p);
 ods output pValues= nt1;
run;

DATA dr_&col;
 MERGE raw_p nt1 (KEEP=test FalseDiscoveryRate RENAME=(FalseDiscoveryRate=fdr&col));
RUN; 

DATA adj_pvalues;
 set adj_pvalues dr_&col;
RUN;

%MEND;

** Create a blank version of the output dataset from the "calcfdr" macro;
DATA adj_pvalues;
RUN;

** Call macro for each sample (combined, boys, girls) and each treatment (low-poverty voucher, traditional voucher);
* Note: The order of disorders for the 7 p-values is: major depressive, bipolar, panic, posttraumatic stress, oppositional-defiant, intermittent explosive, conduct;

%calcfdr(T4. Combined low pov     	,1,0.845,0.006,0.132,0.008,0.146,0.074,0.065) 
%calcfdr(T4. Combined traditional 	,2,0.475,0.000,0.603,0.681,0.658,0.450,0.703)
%calcfdr(T5. Boys low pov			,3,0.011,0.015,0.877,0.002,0.906,0.597,0.000)
%calcfdr(T5. Boys traditional		,4,0.126,0.000,0.863,0.013,0.713,0.838,0.132)
%calcfdr(T5. Girls low pov			,5,0.032,0.152,0.002,0.395,0.012,0.031,0.169)
%calcfdr(T5. Girls traditional		,6,0.027,0.006,0.288,0.190,0.858,0.387,0.003)

** Add disorder name to row labels;
data adj_pvalues;
	set adj_pvalues (where=(rowlab~="")); * remove blank row;
	* add name of disorder; 
	if test=1 then disorder="depression";
	if test=2 then disorder="bipolar";
	if test=3 then disorder="panic";
	if test=4 then disorder="PTSD";
	if test=5 then disorder="ODD";
	if test=6 then disorder="IED";
	if test=7 then disorder="conduct";
run;

** Output the adjusted p-values;
PROC PRINT DATA=adj_pvalues noobs; 
	var rowlab raw_p Test disorder fdr1 fdr2 fdr3 fdr4 fdr5 fdr6;
RUN;










